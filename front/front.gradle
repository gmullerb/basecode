//  Copyright (c) 2018 Gonzalo MÃ¼ller Bravo.
//  Licensed under the MIT License (MIT), see LICENSE.txt

plugins {
  id 'com.moowork.node' version '1.2.0'
}

description = 'Frontend code.'
group = 'Code'

apply from: "$localGradleModuleFolder/code.gradle"

// CONSTANTS
////////////

final SOURCE_DIR = "$projectDir/src"
final MAIN_DIR = "$SOURCE_DIR/main"
final TEST_DIR = "$projectDir/test"
final REPORTS_DIR = "$buildDir/reports"
final TESTS_REPORT_DIR = "$REPORTS_DIR/tests"
final COVERAGE_REPORT_DIR = "$REPORTS_DIR/coverage"

final INCREMENTAL_MAIN_CONFIGURATION = {
  inputs.dir MAIN_DIR
}

final INCREMENTAL_LINT_CONFIGURATION = {
  inputs.files obtainFilesFromProject(project, '.', [FRONT$ESLINT_CFG_FILE])
}

final NO_OUTPUTS_CONFIGURATION = {
  outputs.upToDateWhen { true }
}

final INCREMENTAL_COMMON_TEST_CONFIGURATION = {
  inputs.file 'config/test/common-test.cfg.js'
} << NO_OUTPUTS_CONFIGURATION

final INCREMENTAL_UNIT_TEST_CONFIGURATION = {
  inputs.dir "$TEST_DIR/unit"
  inputs.file 'config/test/unit-test.cfg.js'
} << INCREMENTAL_COMMON_TEST_CONFIGURATION

final INCREMENTAL_INTEGRATION_TEST_CONFIGURATION = {
  inputs.file 'config/test/integration/integration-test.cfg.js'
  inputs.file 'config/test/integration/jasmine.cfg.js'
  inputs.dir "$TEST_DIR/integration"
} << INCREMENTAL_COMMON_TEST_CONFIGURATION

final INCREMENTAL_TEST_LINT_CONFIGURATION = {
  inputs.files obtainFilesFromProject(project, TEST_DIR, [FRONT$ESLINT_CFG_FILE])
} << INCREMENTAL_LINT_CONFIGURATION

final BUILD_TASK = 'build'
final CHECK_TASK = 'check'

ext {
  webDir = "$buildDir/web"
  npmTaskConfiguration = {
    // NpmTask task settings
    args = ['run'] + args + (logger.debugEnabled ? ['--loglevel', 'silly'] : ['--silent'])
    // gradle task settings
    dependsOn += [npmInstall]
    inputs.file file('package.json')
    doFirst  {
      logger.debug "$it.name command: ${args.join ' '}"
    }
  }
  eslintTaskConfiguration = {
    // NpmTask task settings
    args += ["--eslintConfigFile=${resources.text.fromArchiveEntry(configurations.styleConfig, 'front/.eslintrc.json').asFile().path}"]
  } << npmTaskConfiguration
  mainNpmTaskConfiguration = npmTaskConfiguration << INCREMENTAL_MAIN_CONFIGURATION
  eslintMainNpmTaskConfiguration = eslintTaskConfiguration << INCREMENTAL_MAIN_CONFIGURATION
}

// Plugin settings
//////////////////

node {
  version = FRONT$NODE_VERSION
  download = true
}

// TASKS
////////

// Assessment
/////////////

task assessMain(type: NpmTask) {
  // NpmTask task settings
  args = ['eslintMain']
  // gradle task settings
  inputs.files obtainFilesFromProject(project, MAIN_DIR, [FRONT$ESLINT_CFG_FILE])
}
assessMain assessMainCodeConfiguration << eslintMainNpmTaskConfiguration << INCREMENTAL_TEST_LINT_CONFIGURATION <<
  NO_OUTPUTS_CONFIGURATION

task assessUnitTest(type: NpmTask) {
  args = ['eslintUT']
}
assessUnitTest eslintTaskConfiguration << assessUnitTestCodeConfiguration << INCREMENTAL_TEST_LINT_CONFIGURATION <<
  INCREMENTAL_UNIT_TEST_CONFIGURATION

task assessIntegrationTest(type: NpmTask) {
  args = ['eslintIT']
}
assessIntegrationTest eslintTaskConfiguration << assessIntegrationTestCodeConfiguration <<
  INCREMENTAL_TEST_LINT_CONFIGURATION << INCREMENTAL_INTEGRATION_TEST_CONFIGURATION

task assessTest(type: NpmTask) {
  args = ['eslintTest']
}
assessTest eslintMainNpmTaskConfiguration << assessTestCodeConfiguration << INCREMENTAL_TEST_LINT_CONFIGURATION <<
  INCREMENTAL_UNIT_TEST_CONFIGURATION << INCREMENTAL_INTEGRATION_TEST_CONFIGURATION << NO_OUTPUTS_CONFIGURATION

task assessCss(type: NpmTask) {
  // NpmTask task settings
  args = ['stylelint', """--stylelintConfigFile=${resources.text
    .fromArchiveEntry(configurations.styleConfig, 'front/.stylelintrc.json').asFile().path}"""]
  // gradle task settings
  description = 'Analyze and assess CSS files.'
  group = CODE$GROUP_ASSESS
  shouldRunAfter assessMain
  inputs.file '.stylelintrc.json'
  inputs.files obtainFilesFromProject(project, SOURCE_DIR, ['*.css'])
}
assessCss mainNpmTaskConfiguration << NO_OUTPUTS_CONFIGURATION

task assessLocal(type: NpmTask) {
  // NpmTask task settings
  args = ['eslintLocal']
  // gradle task settings
  description = 'Analyze and assess Local code'
  group = CODE$GROUP_ASSESS
  inputs.file FRONT$ESLINT_CFG_FILE
  inputs.files obtainFilesFromProject(project, 'local_js', [FRONT$CODE_FILES, FRONT$ESLINT_CFG_FILE])
  inputs.files obtainFilesFromProject(project, 'local_modules', [FRONT$CODE_FILES, FRONT$ESLINT_CFG_FILE])
}
assessLocal eslintTaskConfiguration << NO_OUTPUTS_CONFIGURATION

assess {
  dependsOn += [assessCss, assessLocal]
}

// Tests
////////

task unitTest(type: NpmTask) {
  // Code task settings
  ext.testReportDir = "$TESTS_REPORT_DIR/unitTest"
  ext.coverageReportDir = COVERAGE_REPORT_DIR
  // NpmTask task settings
  args = ['unitTest'
    , "--front_src_dir=$SOURCE_DIR"
    , "--front_test_report_dir=$testReportDir"
    , "--front_coverage_report_dir=$coverageReportDir"]
  // gradle task settings
  outputs.dir testReportDir
  outputs.dir coverageReportDir
}
unitTest mainNpmTaskConfiguration << unitTestCodeConfiguration << INCREMENTAL_UNIT_TEST_CONFIGURATION

task integrationTest(type: NpmTask) {
  // Code task settings
  ext.reportDir = "$TESTS_REPORT_DIR/integrationTest"
  // NpmTask task settings
  args = ['integrationTest'
    , "--front_src_dir=$SOURCE_DIR"
    , "--front_test_report_dir=$reportDir"]
  // gradle task settings
  outputs.dir reportDir
}
integrationTest mainNpmTaskConfiguration << integrationTestCodeConfiguration << INCREMENTAL_INTEGRATION_TEST_CONFIGURATION

task test
test testCodeConfiguration

task check
check checkCodeConfiguration

// Build
////////

task assemble(type: NpmTask) {
  // NpmTask task settings
  args = ['jsbuild',
          "--front_src=$MAIN_DIR",
          "--front_dest=$webDir/META-INF/resources",
          "--front_env=$runningEnvironment"]
  // gradle task settings
  inputs.file 'local_js/build.js'
  outputs.dir webDir
  shouldRunAfter assessMain
}
assemble mainNpmTaskConfiguration << assembleCodeConfiguration

task jar(type: Jar) {
  // jar task settings
  baseName = FRONT$ARCHIVE_NAME
  version = FRONT$VERSION
  from webDir
  destinationDir = file("$buildDir/libs")
  // gradle task settings
  description = 'Assembles a JAR archive containing the web static resources.'
  group = CODE$GROUP_BUILD
  dependsOn = ['assemble']
  shouldRunAfter CHECK_TASK
}

task "$BUILD_TASK" {
  dependsOn = [CHECK_TASK, 'jar']
}
"$BUILD_TASK" buildCodeConfiguration

// Documentation
////////////////

task doc(type: NpmTask) {
  // NpmTask task settings
  args = ['jsdoc']
  // gradle task settings
  ext.docType = 'jsDoc'
  ext.docDir = "$buildDir/docs"
  inputs.file 'config/jsdoc.cfg.json'
  outputs.dir docDir
}
doc mainNpmTaskConfiguration << docCodeConfiguration
