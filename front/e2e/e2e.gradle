//  Copyright (c) 2018 Gonzalo MÃ¼ller Bravo.
//  Licensed under the MIT License (MIT), see LICENSE.txt

apply plugin: 'com.moowork.node'
description = 'End to end test.'
group = 'Utility'

// CONSTANTS
////////////

final INCREMENTAL_E2E_INFO = {
  inputs.files obtainFilesFromProject(project, 'src', [FRONT$CODE_FILES])
  inputs.files obtainFilesFromProject(project, 'local_js', [FRONT$CODE_FILES])
}
final MAIN_TASK_DESCRIPTION = 'Run the end to end tests.'

// TASKS
////////

task "${CODE$ASSESS_TASK_NAME}"(type: NpmTask) {
  // NpmTask task settings
  args = ['eslintE2E']
  // gradle task settings
  description = 'Analyze and assess End to End test code.'
  group = CODE$GROUP_ASSESS
  inputs.files obtainFilesFromProject(project, '.', [FRONT$ESLINT_CFG_FILE])
  outputs.upToDateWhen { true }
}
"${CODE$ASSESS_TASK_NAME}" npmTaskConfiguration << INCREMENTAL_E2E_INFO

task "${GLOBAL$TEST_TASK_NAME}"(type: NpmTask) {
  // NpmTask task settings
  args = ['e2eTest'
          , "--e2e_dir=$projectDir/src/test"
          , "--e2e_env=$runningEnvironment"
          , "--e2e_url=http://localhost:8080/"
          , "--e2e_jar=${project(':back').fixedJar.archivePath}"
          , "--e2e_report_dir=$buildDir"]
  // gradle task settings
  description = MAIN_TASK_DESCRIPTION
  group = CODE$GROUP_TEST
  dependsOn = [':front:npmInstall', ':back:fixedJar']
  shouldRunAfter = [CODE$ASSESS_TASK_NAME]
  outputs.dir buildDir
  doLast {
    logger.quiet "See e2e test report at $buildDir"
  }
}
"${GLOBAL$TEST_TASK_NAME}" mainNpmTaskConfiguration << INCREMENTAL_E2E_INFO

task "${GLOBAL$CHECK_TASK_NAME}"
"${GLOBAL$CHECK_TASK_NAME}" checkCodeConfiguration
"${GLOBAL$CHECK_TASK_NAME}" {
  dependsOn = [CODE$ASSESS_TASK_NAME, GLOBAL$TEST_TASK_NAME]
}

task "${GLOBAL$RUN_TASK_NAME}" {
  description = MAIN_TASK_DESCRIPTION
  group = GLOBAL$GROUP_RUN
  dependsOn = [GLOBAL$TEST_TASK_NAME]
}

// Plugin settings
//////////////////

node {
  version = FRONT$NODE_VERSION
  download = true
  workDir = parent.node.workDir
  npmWorkDir = parent.node.npmWorkDir
  nodeModulesDir = parent.node.nodeModulesDir
}
