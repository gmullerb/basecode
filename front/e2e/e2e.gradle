//  Copyright (c) 2018 Gonzalo MÃ¼ller Bravo.
//  Licensed under the MIT License (MIT), see LICENSE.txt

apply plugin: 'com.moowork.node'
description = 'End to end test.'
group = 'Utility'

// CONSTANTS
////////////

final INCREMENTAL_E2E_INFO = {
  inputs.files fileLister.obtainFullFileTree("$projectDir/src", [includes: [FRONT$CODE_FILES]])
  inputs.files fileLister.obtainFullFileTree("$projectDir/local_js", [includes: [FRONT$CODE_FILES]])
}
final MAIN_TASK_DESCRIPTION = 'Run the end to end tests.'

// Plugin settings
//////////////////

node {
  version = FRONT$NODE_VERSION
  download = true
  workDir = parent.node.workDir
  npmWorkDir = parent.node.npmWorkDir
  nodeModulesDir = parent.node.nodeModulesDir
}

// TASKS
////////

task assess(type: NpmTask) {
  // NpmTask task settings
  args = ['eslintE2E']
  // gradle task settings
  description = 'Analyze and assess End to End test code.'
  group = CODE$GROUP_ASSESS
  inputs.files fileLister.obtainFullFileTree("$projectDir", [includes: [FRONT$ESLINT_CFG_FILE]])
  outputs.upToDateWhen { true }
}
assess eslintTaskConfiguration << INCREMENTAL_E2E_INFO

task test(type: NpmTask) {
  // NpmTask task settings
  args = ['e2eTest'
          , "--e2e_dir=$projectDir/src/test"
          , "--e2e_env=$runningEnvironment"
          , "--e2e_url=http://localhost:8080/"
          , "--e2e_jar=${project(':back').fixedJar.archivePath}"
          , "--e2e_report_dir=$buildDir"]
  // gradle task settings
  description = MAIN_TASK_DESCRIPTION
  group = CODE$GROUP_TEST
  dependsOn = [':front:npmInstall', ':back:fixedJar']
  shouldRunAfter = [assess]
  outputs.dir buildDir
  doLast {
    logger.quiet "See e2e test report at $buildDir"
  }
}
test mainNpmTaskConfiguration << INCREMENTAL_E2E_INFO

task check
check checkCodeConfiguration
check {
  dependsOn = [assess, test]
}
